program KABA2V1;

{$APPTYPE CONSOLE}
{$R *.res}

uses
  System.SysUtils;

var
  P1, Q1, p, q, k, z, error1, error2: integer;
  proverka1:string;
  { P1 - числитель дроби;
    Q1 - знаменатель дроби;
    p - НОД и длина предпериода;
    z - остаток от деления на 2 и 5(доп. делитель);
    k - переменная для цикла for;
    q - поиск длины периода;
    error1,error2 - число ошибок в проверке
    вводимых данных;
    proverka1 - переменная для проверки вводимых данных. }


begin
  // Вводим значение числителя дроби и проверяем его.
  repeat
    Writeln('Введите, пожалуйста, число P');
    readln(proverka1);
    val(proverka1,P1,error1);
    if (error1>0) or(P1<=0) then
    writeln('Введены некорректные данные');
  until (error1=0) and (P1>0);
  // Вводим значение знаменателя дроби и проверяем его.
    repeat
    Writeln('Введите, пожалуйста, число Q');
    readln(proverka1);
    val(proverka1,Q1,error2);
    if (error2>0) or(Q1<=0) then
    writeln('Введены некорректные данные');
  until (error2=0) and (Q1>0);

  // Выводим на экран целую часть дроби.
  write(P1 div Q1);
  // Ищем остаток и избавляемся от целой части.
  P1 := P1 mod Q1;
  // Если числитель > 0, то есть и дробная часть.
  if P1 > 0
  then
  begin
    // Выводим запятую в записи дробного числа.
    write(',');
    // Находим НОД ( "медленный" алгоритм Евклида) для сокращения дроби,
    // если оно возможно.
    p := P1;
    q := Q1;
    while p <> q do
      if q > p then
        q := q - p
      else
        p := p - q;
    // Сокращаем дробь P1/Q1
    P1 := P1 div p;
    Q1 := Q1 div p;

    // Находим длину предпериода дроби. Используем для этого
    // максимальное количество делителей  2 и 5.

    p := 0; // Длина будущего предпериода.
    q := 0;
    z := Q1; // Будущий остаток знаменателя после деления на 2 и на 5.
    while z mod 2 = 0 do
    begin
      inc(p);
      z := z div 2;
    end;
    while z mod 5 = 0 do
    begin
      inc(q);
      z := z div 5;
    end;
    if p < q then
    p := q;

    // Выводим на экран предпериод дроби
    for k := 1 to p do
    begin
      P1 := P1 * 10;
      write(P1 div Q1);
      P1 := P1 mod Q1;
    end;

    // Если есть период, находим его длину и выводим на экран.
    if z > 1 // Если делители знаменателя не только 2 и 5, то есть период.
    then
    begin
      write('(');
      // Поиск длины периода с поощью девяток.
      q := 9;
      // p - количество разрядов в периоде.
      p := 1;
      while q mod z > 0 do
      begin
        inc(p);
        // Делим 999.. по модулю z, во избежание переполнения.
        q := q mod z * 10 + 9;
      end;
      // Выводим на экран цифры периода.
      for k := 1 to p do
      begin
        P1 := P1 * 10;
        write(P1 div Q1);
        P1 := P1 mod Q1;
      end;
      write(')');
    end;
  end;
  readln;
  readln;

end.
